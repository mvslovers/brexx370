         MACRO                                                          $RTRN
&NAME    $RTRN &RC=0,&RENT=,&SP=1,&SPM=YES,&REST01=YES                  $RTRN
         MNOTE *,'       $RTRN     VERSION 002 05/07/75  04/29/76  GPW' $RTRN
.********************************************************************** $RTRN
.*                                                                    * $RTRN
.* $RTRN                                                              * $RTRN
.*                                                                    * $RTRN
.* FUNCTION       GENERATE CODE REQUIRED TO RETURN CONTROL TO THE     * $RTRN
.*                CALLING PROGRAM.                                    * $RTRN
.*                                                                    * $RTRN
.* DESCRIPTION    STANDARD IBM LINKAGE CODE IS GENERATED BY THE MACRO * $RTRN
.*                TO RETURN CONTROL TO THE CALLING PROGRAM.  THE USER * $RTRN
.*                MAY SPECIFY THE RETURN CODE TO BE RETURNED.         * $RTRN
.*                RE-ENTRANT CODE IS SUPPORTED.                       * $RTRN
.*                                                                    * $RTRN
.*                REGISTERS 0 THROUGH 14 (OR OPTIONALLY, REGISTERS    * $RTRN
.*                2 THROUGH 14) WILL BE RESTORED TO THEIR STATUS      * $RTRN
.*                UPON ENTRY TO THE ROUTINE.  REGISTER 15 WILL BE     * $RTRN
.*                USED FOR A DEFAULT OR USER SPECIFIED RETURN CODE.   * $RTRN
.*                AN X'FF' WILL BE PLACED IN THE HIGH ORDER BYTE OF   * $RTRN
.*                WORD 4 OF THE OLD SAVE AREA TO INDICATE THAT        * $RTRN
.*                CONTROL HAS BEEN RETURNED TO THE CALLING PROGRAM    * $RTRN
.*                                                                    * $RTRN
.*                CODE IS ALSO GENERATED TO RESTORE THE PROGRAM MASK  * $RTRN
.*                TO ITS STATUS UPON ENTRY TO THE ROUTINE.  THE       * $RTRN
.*                VALUE OF THE MASK IS OBTAINED FROM THE HIGH ORDER   * $RTRN
.*                BYTE OF REGISTER 14.  THE ASSUMPTION IS MADE THAT   * $RTRN
.*                STANDARD LINKAGE WAS USED TO INVOKE THE ROUTINE,    * $RTRN
.*                THAT IS, THAT ENTRY WAS MADE BY A  BALR  R14,R15.   * $RTRN
.*                IF THIS IS NOT TRUE, THE PROGRAM MASK WILL BE       * $RTRN
.*                RESTORED INCORRECTLY.  IF SPM=NO IS SPECIFIED, THE  * $RTRN
.*                PROGRAM MASK WILL NOT BE ALTERED.                   * $RTRN
.*                                                                    * $RTRN
.*                IF RE-ENTRANT CODE IS BEING GENERATED, A DROP R2    * $RTRN
.*                STATEMENT IS GENERATED, SO THE USER SHOULD NOT USE  * $RTRN
.*                REGISTER 2 AS A BASE REGISTER.                      * $RTRN
.*                                                                    * $RTRN
.* SYNTAX         NAME     $RTRN     RC=NUM1                          * $RTRN
.*                                      (R15)                         * $RTRN
.*                                   RENT=LEN                         * $RTRN
.*                                   SP=NUM2                          * $RTRN
.*                                   SPM=NO                           * $RTRN
.*                                   REST01=NO                        * $RTRN
.*                                                                    * $RTRN
.*                NAME   - A SYMBOLIC TAG ASSIGNED TO THE FIRST       * $RTRN
.*                         INSTRUCTION GENERATED.                     * $RTRN
.*                                                                    * $RTRN
.*                RC     - SPECIFIES THE RETURN CODE TO BE PLACED IN  * $RTRN
.*                         REGISTER 15.  A NUMBER MAY BE SPECIFIED,   * $RTRN
.*                         OR (R15) INDICATES THAT THE RETURN CODE    * $RTRN
.*                         HAS ALREADY BEEN PLACED IN REGISTER 15 BY  * $RTRN
.*                         THE USER.  THE DEFAULT RETURN CODE IS 0.   * $RTRN
.*                                                                    * $RTRN
.*                RENT   - INDICATES THAT THE WORK AREA OBTAINED BY   * $RTRN
.*                         $ENTER IS TO BE FREED.  LEN SPECIFIES THE  * $RTRN
.*                         LENGTH OF THE AREA.  THE ADDRESS IS        * $RTRN
.*                         OBTAINED FROM REGISTER 13.                 * $RTRN
.*                                                                    * $RTRN
.*                SP     - SPECIFIES THE SUBPOOL OF THE WORK AREA.    * $RTRN
.*                         IT MUST AGREE WITH THE SUBPOOL SPECIFIED   * $RTRN
.*                         IN $ENTER.  THE DEFAULT IS 1.              * $RTRN
.*                                                                    * $RTRN
.*                SPM    - SPM=NO SPECIFIES THAT THE PROGRAM MASK     * $RTRN
.*                         IS NOT TO BE ALTERED.                      * $RTRN
.*                                                                    * $RTRN
.*                REST01 - REST01=NO SPECIFIES THAT THE CONTENTS      * $RTRN
.*                         OF REGISTERS 0 AND 1 ARE NOT TO BE         * $RTRN
.*                         RESTORED.  THIS OPTION SHOULD BE           * $RTRN
.*                         SPECIFIED WHEN DATA IS TO BE PASSED        * $RTRN
.*                         BACK TO THE CALLING PROGRAM IN             * $RTRN
.*                         REGISTERS 0 AND 1.                         * $RTRN
.*                                                                    * $RTRN
.* ERRORS         NO ERROR MESSAGES ARE GENERATED                     * $RTRN
.*                                                                    * $RTRN
.* EXAMPLE        EX1      $RTRN                                      * $RTRN
.*                                                                    * $RTRN
.*                EX2      $RTRN  RC=4                                * $RTRN
.*                                                                    * $RTRN
.*                EX3      $RTRN  RC=(R15),RENT=DSECTLEN              * $RTRN
.*                                                                    * $RTRN
.* GLOBAL SYMBOLS                                                     * $RTRN
.*                                                                    * $RTRN
.*                NONE                                                * $RTRN
.*                                                                    * $RTRN
.* MACROS CALLED                                                      * $RTRN
.*                                                                    * $RTRN
.*                FREEMAIN                                            * $RTRN
.*                                                                    * $RTRN
.* UPDATE SUMMARY                                                     * $RTRN
.*                                                                    * $RTRN
.*      VERSION   DATE     CHANGE                                     * $RTRN
.*                                                                    * $RTRN
.*        002   04/29/76   REST01 KEYWORD ADDED                       * $RTRN
.*                                                                    * $RTRN
.********************************************************************** $RTRN
.*                                                                      $RTRN
         AIF   ('&RENT' EQ '').REGULAR                                  $RTRN
&NAME    LR    R1,R13                   LOAD STORAGE ADDRESS            $RTRN
         BALR  R2,0                     ESTABLISH BASE REG              $RTRN
         USING *,R2                     DEFINE BASE REGISTER            $RTRN
         L     R3,4(R13)                SAVE OLD SAVEAREA ADDR          $RTRN
         LR    R4,R15                   SAVE RETURN CODE                $RTRN
.FREE    MNOTE *,' FREEMAIN R,LV=&RENT,A=(1),SP=&SP'                    $RTRN
         FREEMAIN R,LV=&RENT,A=(1),SP=&SP FREE STORAGE                  $RTRN
* END FREEMAIN - $RTRN                                                  $RTRN
         AIF   ('&RC' NE '(R15)').RESSAV                                $RTRN
         LR    R15,R4                   RESTORE RETURN CODE             $RTRN
.RESSAV  LR    R13,R3                   RESTORE OLD SAVEAREA            $RTRN
         DROP  R2                                                       $RTRN
         AGO   .COMMON                                                  $RTRN
.REGULAR ANOP                                                           $RTRN
&NAME    L     R13,4(R13)               RESTORE OLD SAVEAREA            $RTRN
.COMMON  AIF   ('&RC' EQ '(R15)').RES14                                 $RTRN
         LA    R15,&RC                  SET RETURN CODE                 $RTRN
.RES14   L     R14,12(R13)              RESTORE RETURN ADDRESS          $RTRN
         AIF   ('&REST01' EQ 'NO').REST2                                $RTRN
         LM    R0,R12,20(R13)           RESTORE REGS 0 - 12             $RTRN
         AGO   .SETFLAG                                                 $RTRN
.REST2   LM    R2,R12,28(R13)           RESTORE REGS 2 - 12             $RTRN
.SETFLAG MVI   12(R13),X'FF'            SET RETURNED FLAG               $RTRN
.*-------TEST FOR SPM RESET                                             $RTRN
         AIF   ('&SPM' EQ 'NO').NOSPM                                   $RTRN
         SPM   R14                      RESTORE PROGRAM MASK            $RTRN
.NOSPM   BR    R14                      RETURN                          $RTRN
         MEND                                                           $RTRN
